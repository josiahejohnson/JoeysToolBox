 
Option Explicit

' Constants for type of event log entry
const EVENTLOG_INFORMATION = 4


Dim Args  
Dim swbemLocator, SWbemServices, objCollection, oProviderLocation, oLocation   
Dim strComputerName, arrComputers, objComputer, sCollectionIDs
Dim objDirectRule
Dim strmessage, objshell
Dim seventlog, sClearPxeflag, ClearLastPxe

On Error Resume Next  


'CollectionIDs from which to remove the computer
'Should an eventlog entry be generated, set Seventlog=1

sEventlog = "1" 
ClearLastPxe = "1"
sCollectionIDs = "06000018:0600000A:0600001A"  

'------------------------------------------------------------
'Get Command Line arguments

Set args = WScript.Arguments
strComputername = args.Item(0)


If strComputerName = NULL then
   wscript.quit
End if

'------------------------------------------------------------
'Main script 

set objShell = CreateObject("WScript.Shell")

    Set swbemLocator = CreateObject("WbemScripting.SWbemLocator")
    swbemLocator.Security_.AuthenticationLevel = 6 'Packet Privacy.
    Set swbemServices = swbemLocator.ConnectServer(".", "root\SMS")
    Set oProviderLocation = swbemServices.InstancesOf("SMS_ProviderLocation")
    For Each oLocation In oProviderLocation
        If oLocation.ProviderForLocalSite = True Then
            Set swbemServices = swbemLocator.ConnectServer(oLocation.Machine, "root\sms\site_" + oLocation.SiteCode)
        End If        
    Next 

Set arrComputers = SWbemServices.ExecQuery("select * from SMS_R_System where Name='" & strComputerName & "' and Obsolete = 0")

For Each objComputer In arrComputers
   RemoveCollectionMembership objComputer.ResourceID
	If ClearLastPxe = "1" then
	   ClearPxeAdvertisementResource objComputer.ResourceID
	End if

'Write to eventlog if Seventlog = 1   
   If Seventlog = "1" then
   strMessage = strcomputername & " will be removed from the following collection ID's " & scollectionids
   objShell.LogEvent EVENTLOG_INFORMATION, strMessage
   End IF
   
Next


Set objCollection = Nothing
Set SWbemServices = Nothing
Set SWbemLocator = Nothing


Wscript.Quit


'------------------------------------------------
Sub RemoveCollectionMembership(intresourceid)
on error resume next

Dim mCollectionID, i
mCollectionID = Split (sCollectionIDs, ":")
for i = Lbound(mCollectionID) to UBound(mCollectionID)

	Set objCollection = SWbemServices.Get("SMS_Collection='" & MCollectionID(i) & "'")
	Set ObjDirectRule = SWbemServices.Get("SMS_CollectionRuleDirect").SpawnInstance_
	ObjDirectRule.ResourceID = intresourceid
	ObjCollection.DeleteMembershipRule objDirectRule
	next
End Sub


Sub ClearPxeAdvertisementResource(intresourceid)

   On Error Resume Next

    Dim resources
    Dim InParams
    
    ' Set up the Resource array parameter.
    resources = Array(1)
    resources(0) = intresourceid
    
    Set InParams = swbemServices.Get("SMS_Collection").Methods_("ClearLastNBSAdvForMachines").InParameters.SpawnInstance_
	inParams.ResourceIDs = resources

   swbemServices.ExecMethod "SMS_Collection","ClearLastNBSAdvForMachines", InParams
       
End Sub    


'------------------------------------------------



WScript.Quit(0)  